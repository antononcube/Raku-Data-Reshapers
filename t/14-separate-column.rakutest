use v6.d;

use lib './lib';
use lib '.';

use Data::Reshapers;

use Test;

# See "Anscombe's quartet article :  https://en.wikipedia.org/wiki/Anscombe%27s_quartet .
# The set can be also obtained with example-dataset from "Data::ExampleDatasets" :
#   my @dfAnscombe = |example-dataset('anscombe');

my @dsAnsombe = [
    %('X1' => 10, 'X2' => 10, 'X3' => 10, 'X4' => 8, 'Y1' => 8.04, 'Y2' => 9.14, 'Y3' => 7.46, 'Y4' => 6.58),
    %('X1' => 8, 'X2' => 8, 'X3' => 8, 'X4' => 8, 'Y1' => 6.95, 'Y2' => 8.14, 'Y3' => 6.77, 'Y4' => 5.76),
    %('X1' => 13, 'X2' => 13, 'X3' => 13, 'X4' => 8, 'Y1' => 7.58, 'Y2' => 8.74, 'Y3' => 12.74, 'Y4' => 7.71),
    %('X1' => 9, 'X2' => 9, 'X3' => 9, 'X4' => 8, 'Y1' => 8.81, 'Y2' => 8.77, 'Y3' => 7.11, 'Y4' => 8.84),
    %('X1' => 11, 'X2' => 11, 'X3' => 11, 'X4' => 8, 'Y1' => 8.33, 'Y2' => 9.26, 'Y3' => 7.81, 'Y4' => 8.47),
    %('X1' => 14, 'X2' => 14, 'X3' => 14, 'X4' => 8, 'Y1' => 9.96, 'Y2' => 8.1, 'Y3' => 8.84, 'Y4' => 7.04),
    %('X1' => 6, 'X2' => 6, 'X3' => 6, 'X4' => 8, 'Y1' => 7.24, 'Y2' => 6.13, 'Y3' => 6.08, 'Y4' => 5.25),
    %('X1' => 4, 'X2' => 4, 'X3' => 4, 'X4' => 19, 'Y1' => 4.26, 'Y2' => 3.1, 'Y3' => 5.39, 'Y4' => 12.5),
    %('X1' => 12, 'X2' => 12, 'X3' => 12, 'X4' => 8, 'Y1' => 10.84, 'Y2' => 9.13, 'Y3' => 8.15, 'Y4' => 5.56),
    %('X1' => 7, 'X2' => 7, 'X3' => 7, 'X4' => 8, 'Y1' => 4.82, 'Y2' => 7.26, 'Y3' => 6.42, 'Y4' => 7.91),
    %('X1' => 5, 'X2' => 5, 'X3' => 5, 'X4' => 8, 'Y1' => 5.68, 'Y2' => 4.74, 'Y3' => 5.73, 'Y4' => 6.89)];

my @dsAnsombeLongForm = to-long-format(@dsAnsombe);

my @dsSmall = (11 .. 13 Z <a|b bIc x|y>).map({ %(id => $_[0], some => $_[1]) });
my @dsSmall2 = (11 .. 13 Z <a|b bIc x|y> Z <a b x> Z <b c y>).map({ <id some left right> Z=> $_ })>>.Hash;

plan 4;

## 1
is dimensions(separate-column(@dsAnsombeLongForm, 'Variable', <Variable Set>, sep => ''))[1], 4;

## 2
is separate-column(@dsAnsombeLongForm, 'Variable', <Variable Set>, sep => '')[0].keys.sort,
        <AutomaticKey Set Variable Value>.sort;

## 3
is-deeply
        separate-column(@dsAnsombeLongForm, 'Variable', <Variable Set>, sep => ''),
        separate-column(@dsAnsombeLongForm, from => 'Variable', to => <Variable Set>, sep => '');

## 4
is-deeply
        separate-column(@dsSmall, 'some', <left right>, sep => / '|' | I /).Array,
        @dsSmall2;

done-testing;
